OPERATIONS := <ws*> OPERATION <ws*> (<"|"> <ws*> OPERATION <ws*>)*
OPERATION  := SELECT | FROM | WHERE | LIMIT | DELETE

FROM            := table (<ws+> hint-column)? (<ws+> as-alias)?

SELECT          := <"select:"> <ws*> columns     | <"s:"> <ws*> columns
WHERE           := <"where:"> <ws*> condition    | <"w:"> <ws*> condition    | condition
LIMIT           := <"limit:"> <ws*> number       | <"l:"> <ws*> number       | number
DELETE          := <"delete!"> <ws*> hint-column | <"d!"> <ws*> hint-column

<table>         := child | parent | symbol* | symbol <"."> symbol*
<child>         := "has:" <ws*> symbol+ | "has:" <ws*> symbol <"."> symbol+
<parent>        := "of:"  <ws*> symbol+ | "of:"  <ws*> symbol <"."> symbol+ | symbol+ "^" | symbol <"."> symbol+ "^"



condition       := symbol <ws*> operator <ws*> (string|number) | condition-in
<condition-in>  := symbol <ws*> "in" <ws*>  <"("> strings* <")">
<strings>       := <ws*> string <ws*> <",">?

columns         := column (<","> <ws*> column)*
column          := qualified-column
hint-column     := <"."> symbol

<qualified-column>        := qualified-symbol | qualified-symbol <ws+> as-alias
<qualified-symbol>        := symbol* | symbol <"."> symbol*
<as-alias>                := <"as"> <ws+> alias

alias    := symbol

symbol   := #"[A-Za-z][A-Za-z0-9-_]*"
string   := <"'"> char* <"'">
number   := #"[0-9]+"
ws       := #"\s+"
<char>   := #'[^\']'
operator := "like" | "="