OPERATIONS := OPERATION <whitespace*> (<"|"> <whitespace*> OPERATION <whitespace*>)*
OPERATION  := SELECT | TABLE | LIMIT | GROUP | ORDER | META | DELETE | SET | FUNCTION

SELECT          := <#"s(el(ect)?)?:"> <whitespace+> columns
columns         := column (<","> <whitespace*> column)*
(* TODO: use `token` instead of `string` *)
column          := string | string <whitespace+> <"as"> <whitespace+> alias
alias           := string

TABLE           := table | table <whitespace+> id | table <whitespace+> ids | table <whitespace+> ands | table <whitespace+> ors

ORDER           := <#"o(rder)?:"> <whitespace*> "+"? column

GROUP           := <#"g(roup)?:"> <whitespace*> column (<whitespace+> FUNCTION)?
FUNCTION        := #"(count|min|max|join)" <":"> <whitespace+> column

LIMIT           := <#"l(imit)?:"> <whitespace*> number

META            := ref
ref             := <#"ref(erences)?"> <"?">

DELETE          := <#"delete!">

SET             := <#"set!"> <whitespace*> assignments

id           := number
assignments  := assignment (<whitespace*> assignment)*
assignment   := string <whitespace*> <"="> <whitespace*> (quoted-string|number)

ids         := number (<whitespace*> <","> <whitespace*> number)*
ors         := comparison (<whitespace*> <","> <whitespace*> comparison)*
ands        := comparison (<whitespace+> comparison)*
comparison  := string <whitespace*> <"="> <whitespace*> ids | string <whitespace*> operator <whitespace*> (quoted-string|number) | string "?"  | "!" string "?"


number        := #"[0-9]+"
whitespace    := #"\s+"
table         := partial-token | token <"."> partial-token
operator      := #"<|>|="

quoted-string := <"'"> space-string? <"'"> | <'"'> space-string? <'"'>
space-string  := #"[A-Za-z0-9-_*.@ ]+"
string        := #"[A-Za-z0-9-_*.@]+"
token         := #"[A-Za-z0-9-_]+"
partial-token := #"[A-Za-z0-9-_]*"
