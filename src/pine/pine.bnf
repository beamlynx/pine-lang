OPERATIONS := OPERATION <ws*> (<"|"> <ws*> OPERATION <ws*>)*
OPERATION  := SELECT | TABLE | LIMIT | GROUP | ORDER | DELETE | SET | FUNCTION

SELECT          := <#"s(el(ect)?)?:"> <ws+> columns
columns         := column (<","> <ws*> column)*
alias           := string

column          := qualified-token


TABLE           := qualified-token | qualified-token <ws+> ands | qualified-token <ws+> ors

ORDER           := <#"o(rder)?:"> <ws*> "+"? column

GROUP           := <#"g(roup)?:"> <ws*> column (<ws+> FUNCTION)?
FUNCTION        := #"(count|min|max|join)" <":"> <ws+> column

LIMIT           := <#"l(imit)?:"> <ws*> number

DELETE          := <#"delete!">

SET             := <#"set!"> <ws*> assignments

assignments  := assignment (<ws*> assignment)*
assignment   := string <ws*> <"="> <ws*> (quoted-string|number)

ors         := comparison (<ws*> <","> <ws*> comparison)*
ands        := comparison (<ws+> comparison)*
comparison  := string <ws*> operator <ws*> (quoted-string|number) | string "?"  | "!" string "?"


number        := #"[0-9]+"
ws    := #"\s+"

qualified-token :=  partial-token | token <"."> partial-token | token <ws+> <"as"> <ws+> alias | token <"."> token <ws+> <"as"> <ws+> alias
operator      := #"<|>|="

quoted-string := <"'"> space-string? <"'"> | <'"'> space-string? <'"'>
space-string  := #"[A-Za-z0-9-_*.@ ]+"
string        := #"[A-Za-z0-9-_*.@]+"
token         := #"[A-Za-z0-9-_]+"
partial-token := #"[A-Za-z0-9-_]*"
