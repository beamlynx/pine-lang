OPERATIONS := OPERATION <ws*> (<"|"> <ws*> OPERATION <ws*>)*
OPERATION  := SELECT | TABLE | WHERE | LIMIT | GROUP | ORDER | DELETE | SET | FUNCTION

SELECT          := <#"s(elect)?:"> <ws+> columns
columns         := column (<","> <ws*> column)*
alias           := symbol

TABLE           := qualified-symbol | qualified-symbol <ws+> hint-column

WHERE           := <#"w(here)?:"> <ws*> condition | condition
condition       := symbol <ws*> operator <ws*> (string|number)

ORDER           := <#"o(rder)?:"> <ws*> "+"? column

GROUP           := <#"g(roup)?:"> <ws*> column (<ws+> FUNCTION)?
FUNCTION        := #"(count|min|max|join)" <":"> <ws+> column

LIMIT           := <#"l(imit)?:"> <ws*> number | number

DELETE          := <#"d(elete)!"> <ws+> hint-column

column          := qualified-symbol

SET             := <#"set!"> <ws*> assignments

assignments  := assignment (<ws+> assignment)*
assignment   := symbol <ws*> <"="> <ws*> (string|number)

qualified-symbol :=  partial-symbol | symbol <"."> partial-symbol | symbol <ws+> <"as"> <ws+> alias | symbol <"."> symbol <ws+> <"as"> <ws+> alias

partial-symbol   := symbol*
symbol           := #"[A-Za-z][A-Za-z0-9-_]*"

hint-column := <"."> symbol

string   := <"'"> char* <"'">
number   := #"[0-9]+"
ws       := #"\s+"
char     := #'[^\']'
operator := "like" | #"="