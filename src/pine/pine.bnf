OPERATIONS := <ws*> OPERATION <ws*> (<"|"> <ws*> OPERATION <ws*>)*

OPERATION    := SELECT-PARTIAL | SELECT | TABLE | WHERE-PARTIAL | WHERE | LIMIT| FROM | ORDER-PARTIAL | ORDER | COUNT | DELETE-ACTION | DELETE | UPDATE-ACTION | GROUP


TABLE             := table table-mods

SELECT-PARTIAL    := <select-op> aliased-columns? <",">?
SELECT            := <select-op> aliased-columns

WHERE-PARTIAL     := <where-op>         conditions <","> <ws*> partial-condition  |  <where-op> partial-condition  |  <where-op>
WHERE             := <where-op>         conditions     |     conditions
LIMIT             := <limit-op>         number         |     number
FROM              := <from-op>          alias

ORDER-PARTIAL     := <order-op>         order-columns? <",">?
ORDER             := <order-op>         order-columns

DELETE-ACTION     := <delete-action-op> hint-column
DELETE            := <delete-op>
UPDATE-ACTION     := <update-action-op> update-assignments
COUNT             := <count-op>

GROUP             := <group-op>         group-args

select-op         := (<"select:"> | <"s:">) <ws*>
where-op          := (<"where:">  | <"w:">) <ws*>
limit-op          := (<"limit:">  | <"l:">) <ws*>
from-op           := (<"from:">   | <"f:">) <ws*>
order-op          := (<"order:">  | <"o:">) <ws*>
group-op          := (<"group:">  | <"g:">) <ws*>
delete-action-op  := (<"delete!"> | <"d!">) <ws*>
delete-op         := (<"delete:"> | <"d:">) <ws*>
update-action-op  := (<"update!"> | <"u!">) <ws*>
count-op          := <"count:">

<table>           := symbol* | symbol* <"."> symbol*
table-mods        := (<ws+> table-mod)*
table-mod         := <":"> ("parent"|"child"|"left"|"right") | as-alias | hint-column

group-args          := aliased-columns <ws*> <"=>"> <ws*> aggregate-functions
aggregate-functions := aggregate-function (<","> <ws*> aggregate-function)*
aggregate-function  := "count" | "string_agg" | "sum" | "avg" | "min" | "max"

update-assignments := update-assignment (<","> <ws*> update-assignment)*
update-assignment  := column <ws*> <"="> <ws*> (column|string|number|boolean|null|date)

conditions          := condition (<","> <ws*> condition)*
condition           := column <ws*> (condition-default|condition-in) (<ws*> cast)?

partial-condition   := column <ws*> operator <ws*> | column
<condition-default> := operator <ws*> (column|string|number|boolean|null|date)
<condition-in>      := (in  | not-in) <ws*>  <"("> strings* <")">
cast                := <"::"> ("text"|"uuid")
<strings>           := <ws*> string <ws*> <",">?

aliased-columns := aliased-column (<","> <ws*> aliased-column)*
aliased-column  := column <ws*> | column <ws+> as-alias <ws*> | alias <"."> star
<as-alias>         := <"as"> <ws+> alias

order-columns := order-column (<","> <ws*> order-column)*
order-column  := column | column <ws+> ("desc"|"asc")

hint-column     := <"."> symbol

columns            := column (<","> <ws*> column)*
column             := qualified-symbol
<qualified-symbol> := symbol | alias <"."> symbol

alias    := symbol

symbol   := #"[A-Za-z][A-Za-z0-9-_]*"
string   := <"'"> char* <"'">
date     := <"'"> #"[0-9]{4}-[0-9]{2}-[0-9]{2}" <"'">
number   := #"[0-9]+"
ws              := (space | line-comment | block-comment)+
space           := #"[ \t\r\n]+"
<line-comment>  := <"--"> #'[^\r\n]*' (#"\r?\n" | !#".")
<block-comment> := #"(?s)/\*.*?\*/"
<char>   := #'[^\']'

<operator>     := like | not-like | ilike | not-ilike | does-not-equal | equals | is | is-not | greater-than | less-than
like           := <"like">
not-like       := <"not"> <ws*> <"like">
ilike          := <"ilike">
not-ilike      := <"not"> <ws*> <"ilike">
does-not-equal := <"!"> <ws*> <"=">
equals         := <"=">
greater-than   := <">">
less-than      := <"<">
is             := <"is">
is-not         := <"is"> <ws*> <"not">
in             := <"in">
not-in         := <"not"> <ws*> <"in">
null           := <"null">
boolean        := "true" | "false"
star           := "*"
