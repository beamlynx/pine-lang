OPERATIONS := <ws*> OPERATION <ws*> (<"|"> <ws*> OPERATION <ws*>)*
OPERATION  := SELECT | TABLE | WHERE | LIMIT| FROM | ORDER | COUNT | DELETE | DELETE-CASCADE

TABLE           := table (<ws+> hint-column)? (<ws+> as-alias)?

SELECT          := <"select:"> <ws*> aliased-columns | <"s:"> <ws*> aliased-columns
WHERE           := <"where:">  <ws*> condition       | <"w:"> <ws*> condition | condition
LIMIT           := <"limit:">  <ws*> number          | <"l:"> <ws*> number    | number
FROM            := <"from:">   <ws*> alias           | <"f:"> <ws*> alias
ORDER           := <"order:">  <ws*> order-columns   | <"o:"> <ws*> order-columns
COUNT           := <"count:">
DELETE          := <"delete!"> <ws*> hint-column     | <"d!"> <ws*> hint-column
DELETE-CASCADE  := <"delete-cascade!"> <ws*> hint-column

<table>         := child | parent | symbol* | symbol <"."> symbol*
<child>         := "has:" <ws*> symbol+ | "has:" <ws*> symbol <"."> symbol+
<parent>        := "of:"  <ws*> symbol+ | "of:"  <ws*> symbol <"."> symbol+ | symbol+ "^" | symbol <"."> symbol+ "^"

condition       := symbol <ws*> operator <ws*> (column|string|number|null) | condition-in
<condition-in>  := symbol <ws*> in <ws*>  <"("> strings* <")">
<strings>       := <ws*> string <ws*> <",">?

aliased-columns := aliased-column (<","> <ws*> aliased-column)*
aliased-column  := column | column <ws+> as-alias

order-columns := order-column (<","> <ws*> order-column)*
order-column  := column | column <ws+> ("desc"|"asc")

hint-column     := <"."> symbol

columns            := column (<","> <ws*> column)*
column             := qualified-symbol
<qualified-symbol> := symbol* | alias <"."> symbol*
<as-alias>         := <"as"> <ws+> alias

alias    := symbol

symbol   := #"[A-Za-z][A-Za-z0-9-_]*"
string   := <"'"> char* <"'">
number   := #"[0-9]+"
ws       := #"\s+"
<char>   := #'[^\']'

<operator>     := like | does-not-equal | equals | is | is-not
like           := <"like">
does-not-equal := <"!"> <ws*> <"=">
equals         := <"=">
is             := <"is">
is-not         := <"is"> <ws*> <"not">
in             := <"in">
null           := <"null">

